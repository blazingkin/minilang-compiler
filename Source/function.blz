require ast/block_node
require declaration
require types/function_type
require types/type_factory
require types/type_error
require types/primitive_type
require tables/symbol_table
require syntax_exception
require cfg/cfg_node
require instructions/instruction
require instructions/arm_instruction
require instructions/register
require interference_graph/interference_node
require minilang_compiler
import Set
import DataStructures
import Lists


constructor Function(line, id, return_type, body, parameters, declarations)

    :show
        "<Function " + id + " " + line + " " + typ + "\n\t" + parameters + " " + declarations  + "\n\t" + body  + ">"
    end

    input_types = parameters.map(x -> x.typ)
    typ = FunctionType(input_types, return_type)

    :typecheck(id_map, struct_map)
        # bind local variables in the id_map
        for i = 0; i < parameters.length!(); i++
            id_map[parameters[i].id] = parameters[i]
        end
        for i = 0; i < declarations.length!(); i++
            id_map[declarations[i].id] = declarations[i]
        end
        symbol_table = SymbolTable(id_map, typ)
        if return_type != primitive_type("void") && return_equivalence? (body).negate()
            print("Function "+id+" is not void and does not return in all cases!")
            primitive_type("error")
        end
        result = body.typecheck(symbol_table, struct_map)
    end

    :copy
        this
    end

    :blocks_in_function(ext)
        result = Set([ext])
        result_length = 0
        while result_length != result.size()
            result_length = result.size()
            entries = result.values()
            for i = 0; i < entries.length!(); i++
                for j = 0; j < entries[i].predecessors.length(); j++
                    result.add(entries[i].predecessors[j])
                end
            end
        end
        return result
    end

    :emit_arm
        function_entry_cfg = CFGNode()
        function_entry_cfg.sealed = true
        function_exit_cfg = CFGNode()
        param_registers = []
        # Move variables from registers / stack to 
        # Emit the stack if we are using it
        for i = 0; i < parameters.length!(); i++
            if i < 4
                # Copy parameter from r_i into local register
                local_register = VirtualRegister(parameters[i].typ, parameters[i].id, "stack")
                function_entry_cfg.add_instruction(Instruction("add", [ARMRegister(i), 0], [local_register], parameters[i].typ))
                param_registers[i] = local_register
            else
                # Copy parameter from stack into local register
                param_registers[i] = VirtualRegister(parameters[i].typ, parameters[i].id, "stack")
            end
        end
        for i = 0; i < declarations.length(); i++
            local_register = VirtualRegister(declarations[i].typ, declarations[i].id, "stack")
            function_entry_cfg.add_instruction(Instruction("Allocation", [], [local_register], declarations[i].typ))
        end
        # Set up bindings in the top block
        for i = 0; i < parameters.length!(); i++
            function_entry_cfg.write_variable(parameters[i].id, param_registers[i])
        end

        # Translate the graph into instructions
        last_node = body.translate_stmt(function_entry_cfg, function_exit_cfg)
        function_exit_cfg.seal()
        if last_node.is_conditional?().negate() && last_node.successor == {blz.nil}
            last_node.set_successor(function_exit_cfg)
        end

        # Get all of the blocks in the function
        function_blocks = blocks_in_function(function_exit_cfg).values()


        if {arguments}.contains?("-constant-prop")
            used_registers = used_llvm_registers(function_blocks).values()
            sparse_simple_constant_propogation!(used_registers, function_blocks)
        end
        if {arguments}.contains?("-dead-code")
            mark_dead_code!(function_blocks)
            eliminate_dead_code!(function_blocks)
        end

        # Translate phi instructions into psuedo-phis
        for i = 0; i < function_blocks.length(); i++
            function_blocks[i].resolve_phis_arm()
        end

        # Generate the arm instructions
        for i = 0; i < function_blocks.length(); i++
            function_blocks[i].generate_arm()
        end

        # Generate gen and kill sets for register allocation
        allocate_registers_arm(function_blocks)

        function_entry_cfg.add_arm_instruction_at_beginning(ARMInstruction("push", [], ["{r4-r10}"]))
        function_entry_cfg.add_arm_instruction_at_beginning(ARMInstruction("add", [ARMRegister(13), 4 + (spill_count * 4)], [ARMRegister(11)]))
        function_entry_cfg.add_arm_instruction_at_beginning(ARMInstruction("push", [], ["{fp, lr}"]))
        function_exit_cfg.add_arm_instruction_at_beginning(ARMInstruction("pop", [], ["{fp, pc}"]))
        function_exit_cfg.add_arm_instruction_at_beginning(ARMInstruction("sub", [ARMRegister(11), 4 + (spill_count * 4)], [ARMRegister(13)]))
        function_exit_cfg.add_arm_instruction_at_beginning(ARMInstruction("pop", [], ["{r4-r10}"]))

        return id + ":\n" + function_entry_cfg.emit_arm() + "\n"
    end

    :mark_dead_code(function_blocks)
        work_list = Set()
        for i = 0; i < function_blocks.length(); i++
            block = function_blocks[i]
            for j = 0; j < block.instructions.length(); j++
                instruction = block.instructions[j]
                # Mark only critical instructions
                instruction.marked = (instruction.instruction_type == "Branch" || instruction.instruction_type == "Return" || instruction.instruction_type == "Store" || instruction.instruction_type == "FunctionCall")
                if instruction.marked
                    work_list.add(instruction)
                end
            end
            conditional_register = block.conditional_register
            if conditional_register.source != {blz.nil}
                instruction = conditional_register.source
                instruction.marked = true
                work_list.add(instruction)
            end
        end
        while work_list.size() > 0
            instr = work_list.pick()
            work_list.remove(instr)
            sources = instr.sources.filter(x -> x.type == "VirtualRegister")
            for i = 0; i < sources.length(); i++
                reg = sources[i]
                src = reg.source
                if src != {blz.nil} && src.marked.negate()
                    src.marked = true
                    work_list.add(src)
                end
            end
        end
    end

    :eliminate_dead_code(function_blocks)
        for i = 0; i < function_blocks.length(); i++
            result = []
            block = function_blocks[i]
            for j = 0; j < block.instructions.length(); j++
                instr = block.instructions[j]
                if instr.marked
                    result.add!(instr)
                end
            end
            block.set_instructions(result)
        end
    end

    :used_llvm_registers(function_blocks)
        result = Set()
        register_filter = (x -> x.type == "VirtualRegister")
        for i = 0; i < function_blocks.length(); i++
            block = function_blocks[i]
            for j = 0; j < block.instructions.length(); j++
                instruction = block.instructions[j]
                dest_registers = instruction.destinations.filter(x -> x.type == "VirtualRegister")
                src_registers = instruction.sources.filter(x -> x.type == "VirtualRegister")
                result = result.union(Set(dest_registers))
                result = result.union(Set(src_registers))
            end
            for j = 0; j < block.phis.length(); j++
                phi = block.phis[j]
                result = result.union(Set(phi.destinations))
            end
        end
        return result
    end

    top = "TOP"
    bottom = "BOTTOM"
    :sparse_simple_constant_propogation(registers, function_blocks)
        worklist = Set()
        values = {}
        
        for i = 0; i < registers.length(); i++
            values[registers[i]] = top
        end
        for i = 0; i < registers.length(); i++
            if registers[i].place == "global" || registers[i].place == "stack"
                values[registers[i]] = bottom
            else
                if registers[i].source != {blz.nil}
                    values[registers[i]] = registers[i].source.evaluate(values)
                    if values[registers[i]] != top
                        worklist.add(registers[i])
                    end
                end
            end
        end
        while worklist.size() > 0
            reg = worklist.pick()
            worklist.remove(reg)
            uses = reg.uses
            for i = 0; i < uses.length(); i++
                dests = uses[i].destinations
                for j = 0; j < dests.length(); j++
                    dest = dests[j]
                    if values[dest] != bottom
                        temp = values[dest]
                        values[dest] = uses[i].evaluate(values)
                        if values[dest] != temp
                            worklist.add(dest)
                        end
                    end
                end
            end
        end
        rewrite_uses(function_blocks, values)    
    end

    :rewrite_uses(function_blocks, values)
        for i = 0; i < function_blocks.size(); i++
            block = function_blocks[i]
            new_instrs = []
            for j = 0; j < block.instructions.length(); j++
                instr = block.instructions[j]
                for k = 0; k < instr.sources.length(); k++
                    src = instr.sources[k]
                    if values.has_key?(src) && values[src] != bottom && values[src] != top
                        instr.set_n_source(k, values[src])
                    end
                end
            end
        end
    end

    spill_count = 0
    :allocate_registers_arm(function_blocks)
        for i = 0; i < function_blocks.length(); i++
            function_blocks[i].create_gen_kill_sets()
        end
        changed = true
        while changed
            changed = false
            for i = 0; i < function_blocks.length(); i++
                changed = changed || function_blocks[i].generate_live_out_sets()
            end
        end
        graph = generate_interference_graph!(function_blocks)
        stack = deconstruct_graph!(graph)
        colored_graph = reconstruct_graph!(stack)
        colorings = {}
        for i = 0; i < colored_graph.length(); i++
            if colored_graph[i].color == {blz.nil}
                colorings[colored_graph[i].register] = SpillRegister(spill_count)
                spill_count++
            else
                colorings[colored_graph[i].register] = colored_graph[i].color
            end
        end
        replace_with_colorings!(function_blocks, colorings)
        resolve_spills!(function_blocks)
    end

    :replace_with_colorings(function_blocks, colorings)
        for i = 0; i < function_blocks.length(); i++
            block = function_blocks[i]
            for j = 0; j < block.arm_instruction_list.size(); j++
                instruction = block.arm_instruction_list[j]
                instruction.replace_registers(colorings)
            end
        end
    end

    :spill_location(num)
        4 + (4 * num)
    end

    :resolve_spills(function_blocks)
        for i = 0; i < function_blocks.length(); i++
            block = function_blocks[i]
            orig_instructions = block.get_arm_instructions()
            result = []
            for j = 0; j < orig_instructions.length(); j++
                instr = orig_instructions[j]
                # Solve spills in the source registers
                spills_used = 0
                spill_registers = [ARMRegister(10), ARMRegister(9)]
                for k = 0; k < instr.sources.length(); k++
                    src = instr.sources[k]
                    if src.type == "SpillRegister"
                        spill_to = spill_registers[spills_used]
                        spills_used++
                        result.add!(ARMInstruction("ldr", [ARMRegister(11), spill_location(src.number)], [spill_to]))
                        instr.set_k_source(k, spill_to)
                    end
                end
                if instr.destinations.length() > 0 && instr.destinations[0].type == "SpillRegister"
                    spill_to = ARMRegister(10)
                    spill_num = instr.destinations[0].number
                    instr.set_dest_zero(spill_to)
                    result.add!(instr)
                    result.add!(ARMInstruction("str", [ARMRegister(11), spill_location(spill_num)], [spill_to]))
                else
                    result.add!(instr)
                end            
            end
            block.set_arm_instructions(result)
        end
    end

    # Graph -> Stack of registers + edges
    :deconstruct_graph(graph)
        result = Stack()
        while graph.size() > 0
            # Order lists for removal, highest priority to remove is the first
            node = graph.least()
            position = graph.index_of(node)
            result.push(node)
            neighbors = node.edges.values()
            for i = 0; i < neighbors.length(); i++
                neighbors[i].remove_edge_from_this(node)
            end
            graph.remove!(position)
        end
        result
    end

    # Stack of registers + edges -> Colored Graph
    :reconstruct_graph(stack)
        possible_registers = range(9).map(x -> ARMRegister(x))
        spilling = false
        new_graph = []
        while stack.size() > 0
            node = stack.pop()
            new_graph.add!(node)
            if node.register.type == "ARM_Register"
                node.color = node.register
            else
                # Try to color the node
                not_found = true
                for i = possible_registers.length() - 1; i >= 0 && not_found; i--
                    neighbors = node.edges.values()
                    color = possible_registers[i]
                    can_color = true
                    for j = 0; j < neighbors.length(); j++
                        # If any of the neighbors are not compatible, then we can not color
                        if neighbors[j].is_compatible?(color).negate()
                            can_color = false
                        end
                    end
                    if can_color
                        node.color = color
                        not_found = false
                    end
                end
                if node.color == {blz.nil}
                    spilling = true
                    node.spilled = true
                end
            end
        end
        return new_graph
    end

    :emit_llvm
        llvm = "define " + return_type + " @" + id + "("
        param_registers = []
        for i = 0; i < parameters.length(); i++
            if i != 0
                llvm = llvm + ", "
            end
            param_registers[i] = VirtualRegister(parameters[i].typ, "_P_"+parameters[i].id, "stack")
            llvm = llvm + parameters[i].typ + " " + param_registers[i].emit_llvm()
        end
        llvm = llvm + ")\n{"
        function_entry_cfg = CFGNode()
        function_entry_cfg.sealed = true
        function_exit_cfg = CFGNode()
        if stack_based?()
            # Emit the stack if we are using it
            for i = 0; i < parameters.length!(); i++
                local_register = VirtualRegister(parameters[i].typ, parameters[i].id, "stack")
                function_entry_cfg.add_instruction(Instruction("Allocation", [], [local_register], parameters[i].typ))
                function_entry_cfg.add_instruction(Instruction("Store", [param_registers[i], local_register], [], parameters[i].typ))
            end
            for i = 0; i < declarations.length(); i++
                local_register = VirtualRegister(declarations[i].typ, declarations[i].id, "stack")
                function_entry_cfg.add_instruction(Instruction("Allocation", [], [local_register], declarations[i].typ))
            end
        else
            # Otherwise, set up bindings in the top block
            for i = 0; i < parameters.length!(); i++
                function_entry_cfg.write_variable(parameters[i].id, param_registers[i])
            end
        end
        last_node = body.translate_stmt(function_entry_cfg, function_exit_cfg)
        function_exit_cfg.seal()
        if last_node.is_conditional?().negate() && last_node.successor == {blz.nil}
            last_node.set_successor(function_exit_cfg)
        end
        function_exit_cfg.add_instruction(Instruction("Return", [return_type.default_value()], [], return_type))
        
                # Get all of the blocks in the function
        function_blocks = blocks_in_function(function_exit_cfg).values()

        if {arguments}.contains?("-constant-prop")
            used_registers = used_llvm_registers(function_blocks).values()
            sparse_simple_constant_propogation!(used_registers, function_blocks)
        end
        if {arguments}.contains?("-dead-code")
            mark_dead_code!(function_blocks)
            eliminate_dead_code!(function_blocks)
        end
        
        llvm = llvm + function_entry_cfg.emit_llvm() + "\n}"
        return llvm
    end

    # [CFGNodes] -> [InterferenceNodes]
    :generate_interference_graph(function_blocks)
        interference_graph = {}
        for n = 0; n < function_blocks.length(); n++
            cur_block = function_blocks[n]
            for i = 0; i < cur_block.arm_instruction_list.length(); i++
                cur_instruction = cur_block.arm_instruction_list[i]
                for j = 0; j < cur_instruction.destination_registers.length(); j++
                    if (interference_graph.has_key? (cur_instruction.destination_registers[j])).negate()
                        interference_graph [cur_instruction.destination_registers[j]] = InterferenceNode(cur_instruction.destination_registers[j])
                    end
                end
                for j = 0; j < cur_instruction.source_registers.length(); j++ 
                    if (interference_graph.has_key? (cur_instruction.source_registers[j])).negate()
                        interference_graph [cur_instruction.source_registers[j]] = InterferenceNode(cur_instruction.source_registers[j])
                    end
                end
            end
        end
        for n = 0; n < function_blocks.length(); n++
            cur_block = function_blocks[n]
            cur_live_set = cur_block.live_out
            for i = cur_block.arm_instruction_list.length() - 1; i >= 0 ; i--
                cur_instruction = cur_block.arm_instruction_list[i]
                for j = 0; j < cur_instruction.destination_registers.length(); j++
                    cur_target = cur_instruction.destination_registers[j]
                    cur_live_set.remove(cur_target)
                    cur_live_set_values = cur_live_set.values()
                    for k = 0; k < cur_live_set_values.length(); k++
                        e = cur_live_set_values[k]
                        g = interference_graph[e]
                        interference_graph[cur_target].add_edge(g)
                    end
                end
                for j = 0; j < cur_instruction.source_registers.length(); j++ 
                    cur_live_set.add(cur_instruction.source_registers[j])
                end
            end
        end
        interference_graph.values()
    end

end

:return_equivalence? (node)
    if node.type == "ReturnNode"
        return true
    else if node.type == "IfNode"
        return return_equivalence? (node.main) && return_equivalence? (node.els)
    else if node.type == "BlockNode"
        statements = node.subnodes
        for i = 0; i < statements.length! (); i++
            if return_equivalence? (statements [i])
                if i != statements.length! () - 1
                    print("Dead code in block starting on line " + statements[i].line)
                    primitive_type("error")
                end
                return true
            end
        end
    end
    false
end

:parse_function(json)
    line = json["line"]
    id = json["id"]
    return_type = parse_type(json["return_type"])
    body = parse_block_node(json["body"])
    parse_param = (x -> parse_declaration(x, "stack"))
    parameters = json["parameters"].map!(parse_param)
    declarations = json["declarations"].map!(parse_param)
    Function!(line, id, return_type, body, parameters, declarations)
end
