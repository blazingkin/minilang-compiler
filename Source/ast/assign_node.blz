require ast_factory
require lvalue
require ../types/primitive_type
require ../instructions/register
require ../instructions/instruction
require ../minilang_compiler

constructor AssignNode(target, right, line)
    statement? = true
    expression? = false
    :show
        "<Assign " + target + " to " + right + ">"
    end

    :typecheck(symbol_table, struct_map)
        target_type = target.typecheck(symbol_table, struct_map)
        right_type = right.typecheck(symbol_table, struct_map)
        if right_type.can_not_become(target_type)
            print("Type error on line " + line +": right side of assignment is " + right_type + " expected " + target_type +" instead")
            return primitive_type("error")
        end
        target_type
    end

    :translate_stmt(current_block, exit_block)
        # generate the expression to be stored
        right_val = right.translate(current_block)
        if target.type != "LValue"
            throw "The target of an assignment must be an lvalue"
        end
        if target.terminal
            if stack_based?() || target.place == "global"
                # Get the destination register from the target
                dest_register = target.translate_right(current_block)
                # There is no dot, simply generate a store instruction
                current_block.add_instruction(Instruction("Store", [right_val], [dest_register], target.this.symbol_type))
            else
                current_block.write_variable(target.id, right_val)
            end
        else
            # There is a dot
            dest_register = target.translate_right(current_block)
            current_block.add_instruction(Instruction("Store", [right_val], [dest_register], target.this.right_type))
        end
        current_block
    end

end

:parse_assign_node(json)
    target = parse_lvalue(json["target"])
    right = create_node!(json["source"])
    
    line = json["line"]
    AssignNode(target, right, line)
end